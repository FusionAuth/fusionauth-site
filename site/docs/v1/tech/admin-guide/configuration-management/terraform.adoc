---
layout: doc
title: Terraform
description: Manage FusionAuth configuration changes over time with Terraform.
navcategory: admin
prerequisites: FusionAuth
technology: Terraform
---

== Overview

In this section we dig deeper in the the community supported, open source link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/[FusionAuth Terraform Provider] with which you will be able to manage FusionAuth using Terraform, an open source infrastructure as code automation tool.

* <<Prerequisites>>
* <<Initial Setup>>
** <<FusionAuth API Key>>
** <<Initializing Terraform Working Directory>>
* <<Terraform Import>>
** <<Importing The Default Tenant>>
** <<Importing The FusionAuth Application>>
** <<Terraform Data Source Instead Of Import>>
* <<Terraform Create>>
* <<Terraform Update>>
* <<Terraform Remove>>

== Prerequisites

In order for you to get the most value from this guide, you should have a FusionAuth instance set up for a user to log in with.

If you don't have this set up yet, please review the following link:

* link:/docs/v1/tech/5-minute-setup-guide[5 minute setup guide] - get a FusionAuth instance up and running with a simple application

== Initial Setup

In the initial setup we go through the initializing of the Terraform working directory with the FusionAuth API key.

=== FusionAuth API Key

Create an unlimited super user API Key as described in link:/docs/v1/tech/apis/authentication#managing-api-keys[Managing API Keys] by not selecting any endpoint methods to create a super user key. A super user API Key has access to all API endpoints. Copy the key before pressing the Save button.
//TODO: screenshot http://fusionauth-terraform:9011/admin/api-key/

=== Initializing Terraform Working Directory

With the copied API Key from your fusionauth environment we initialize the Terraform working directory. By creating a new directory.

[source]
----
mkdir your.fusionauth.domain
cd your.fusionauth.domain
----

And add the link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/[FusionAuth Terraform Provider] configuration in the directory by:

* Open the link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs[FusionAuth Terraform Provider Documentation]
* Click USE PROVIDER and Copy Information
* Create the `main.tf` Terraform configuration with the copied information
* Add the previously created key as api_key and add the host as configuration options
* Make sure host is the full URL including protocol

[source]
----
terraform {
  required_providers {
    fusionauth = {
      source = "gpsinsight/fusionauth"
      version = "0.1.91"
    }
  }
}

provider "fusionauth" {
  api_key = "yqTs2LSMd23DYgwbPBA_AxmXHm7_qNFT9jbi3wL9Jr65ymNkNnFisAvjY"
  host = "https://your.fusionauth.domain"
}
----

Finally, the following command performs several different initialization steps in order to prepare the current working directory for use with Terraform.

[source]
----
terraform init
----

It is always safe to run link:https://developer.hashicorp.com/terraform/cli/commands/init[init] multiple times, to bring the working directory up to date with changes in the configuration. Though subsequent runs may give errors, this command will never delete your existing configuration or state.

== Terraform Import

Before we go in to Creating, Updating and Removing of resources we first look in to the link:https://developer.hashicorp.com/terraform/language/import[Import] functionality. If you have existing resources you want to manage via terraform you need to import the current configuration via import to be able to manage it via Terraform.
//Additional input for documentation tbd. import cli https://developer.hashicorp.com/terraform/cli/import import config https://developer.hashicorp.com/terraform/language/import https://spacelift.io/blog/importing-exisiting-infrastructure-into-terraform  https://medium.com/swlh/importing-existing-infrastructure-into-terraform-a6ae168ad2bb https://stackoverflow.com/questions/47613926/import-all-resources-defined-in-tf-file https://www.bitslovers.com/terraform-import/)

// The import command works and i've tested it, but the import configuration has a issue https://github.com/gpsinsight/terraform-provider-fusionauth/issues/214
=== Importing The Default Tenant
We start by importing the default tenant based on the link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/tenant[Terraform Resource: Tenant] by adding the following code below to `import.tf`.

Why importing default tenant is good practice? Manage FusionAuth application related topics is always part of the default tenant, as well as there is not necessarily a need to create a different tenant as you create multiple applications.
//knowledge source https://fusionauth.io/community/forum/topic/1725/what-are-the-pros-and-cons-of-using-the-default-tenant

Create an `import.tf` configuration file with the following content:

[source]
----
import {
  to = fusionauth_tenant.default
  id = "374eba67-12b0-4957-2a4e-a91a8c6f9bfb"
}
----

And link:https://developer.hashicorp.com/terraform/language/import/generating-configuration[generate] a tenant configuration file from it with the following command which you then can apply:

[source]
----
terraform plan -generate-config-out=generated_resources.tf
terraform apply
----

=== Importing The FusionAuth Application
The same works with importing the default FusionAuth application based on the link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/application[Terraform Resource: Application] by adding the following code to `import.tf`.

[source]
----
import {
  to = fusionauth_application.FusionAuth
  id = "3c219e58-ed0e-4b18-ad48-f4f92793ae32"
}
----

=== Terraform Data Source Instead Of Import

Instead of importing a ressource you can use existing configuration as a Data Source instead, the list of all supported data sources you can find in the link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs[FusionAuth Terraform Provider Documentation]

If you choose to manage the default Tenant and FusionAuth Application outside instead as shown previously inside the Terraform configuration. But still add Applications in to the default Tenant via Terraform, you can reference it in the configuration as a data source.

* link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/data-sources/tenant[Terraform Data Source: Application]
* link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/data-sources/application[Terraform Data Source: Application]

[source]
----
data "fusionauth_tenant" "default" {
  name = "Default"
}

data "fusionauth_application" "FusionAuth" {
  name = "FusionAuth"
}
----

== Terraform Create

To create a new resource you can go through the list of resources available to you in the link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs[FusionAuth Terraform Provider Documentation] and pick the ressource you are interested in. Each resource contains information about required and optional arguments.

In this example we create an Application called forum in the default Tenant with related Roles (admin and user), Users (forum-user1 and forum-admin1) and according Registrations:

* link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/application[Terraform Resource: Application]
* link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/application_role[Terraform Resource: Application Role]
* link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/user[Terraform Resource: User]
* link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/registration[Terraform Resource: Registration]

For referenced IDs which are Data Sources use a leading `data.` for the specification e.g. `tenant_id = data.fusionauth_tenant.default.id`.

[source]
----
resource "fusionauth_application" "forum" {
  tenant_id = data.fusionauth_tenant.default.id
  name = "forum"
}

resource "fusionauth_application_role" "forum_admin_role" {
  application_id = fusionauth_application.forum.id
  is_default     = false
  is_super_role  = true
  name           = "admin"
}

resource "fusionauth_application_role" "forum_user_role" {
  application_id = fusionauth_application.forum.id
  is_default     = true
  is_super_role  = false
  name           = "user"
}

resource "fusionauth_user" "forum-user1" {
  email                    = "forum-user1@email.internal"
  first_name               = "John"
  last_name                = "Doe"
  middle_name              = "William"
  password_change_required = false
  password                 = "%WLTvrsYELsyPqC^R7FMUNxt##VyDf6XaWk2R7!gS$oL76Ww"
  username_status          = "ACTIVE"
}

resource "fusionauth_user" "forum-admin1" {
  email                    = "forum-admin1@email.internal"
  first_name               = "John"
  last_name                = "Doe"
  middle_name              = "William"
  password_change_required = false
  password                 = "@CfosPAVT3&hCzz5c^&#2F5BxNUY$X!@s!7Wx9bd6Yon54e3"
  username_status          = "ACTIVE"
}

resource "fusionauth_registration" "forum-admin1-admin-role" {
  user_id        = fusionauth_user.forum-admin1.id
  application_id = fusionauth_application.forum.id
  roles          = ["admin"]
}

resource "fusionauth_registration" "forum-user1-user-role" {
  user_id        = fusionauth_user.forum-user1.id
  application_id = fusionauth_application.forum.id
  roles          = ["user"]
}
----

Once you are happy with your configuration run `terraform plan` and if you are not experiencing errors and are ok with the planned changes you can go ahead with `terraform apply`.

== Terraform Update

Once a resource is either imported or created you can changing existing attributes accordingly in your existing `.tf` files or add additional ones according to the documentation.

* link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/tenant[Terraform Resource: Tenant]
* link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/application[Terraform Resource: Application]
* link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/application_role[Terraform Resource: Application Role]
* link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/user[Terraform Resource: User]
* link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/registration[Terraform Resource: Registration]

If you want to know what already has been defined by terraform but not specified in your `.tf` files you can run `terraform show`.

The configuration can get very large and if you want to show only very specific resources you can list all resources with `terraform state list` and show the resource state with `terraform state show <resource-name>` accordingly.

== Terraform Remove

If you want to remove a resource you can comment or delete the lines accordingly. Run `terraform plan` and `terraform apply` once modified.
