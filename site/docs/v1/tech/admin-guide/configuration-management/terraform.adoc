---
layout: doc
title: Managing FusionAuth Using Terraform
description: Manage FusionAuth configuration changes over time with Terraform.
navcategory: admin
prerequisites: FusionAuth
technology: Terraform
---

== Overview

In this section you'll learn more about the community supported, open source https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/[FusionAuth Terraform Provider], with which you will be able to manage FusionAuth using Terraform, an open source infrastructure as code automation tool.

Since it is a community project, it is not as complete as the client libraries, but it covers the majority of use cases. If you find yourself needing to manage an unsupported resource or configuration, please submit a PR to the https://github.com/gpsinsight/terraform-provider-fusionauth[project using GitHub]. PRs are regularly reviewed and merged.

* <<Prerequisites>>
* <<Initial Setup>>
** <<FusionAuth API Key>>
** <<Initializing Terraform Working Directory>>
* <<Terraform Import>>
** <<Importing The Default Tenant>>
** <<Importing The FusionAuth Application>>
** <<Terraform Data Source Instead Of Import>>
* <<Terraform Create>>
* <<Terraform Update>>
** <<Ignore Lifecycle Changes>>
* <<Terraform Remove>>
** <<Prevent Destroy>>

== Prerequisites

In order to get the most value from this guide, you should have a running FusionAuth instance and the Terraform CLI installed.

If you don't have this set up yet, please review the following links:

* link:/docs/v1/tech/5-minute-setup-guide[5 minute setup guide] - get a FusionAuth instance up and running with a simple application.
* The https://developer.hashicorp.com/terraform/tutorials/docker-get-started/install-cli[Install Terraform] section in the https://developer.hashicorp.com/terraform/tutorials/docker-get-started[Get Started - Docker] guide from Terraform will walk you through installing the Terraform CLI.

== Initial Setup

In the initial setup we go through the initializing of the Terraform working directory with the FusionAuth API key.

=== FusionAuth API Key

Create an unlimited super user API Key as described in link:/docs/v1/tech/apis/authentication#managing-api-keys[Managing API Keys] by not selecting any endpoint methods to create a super user key. A super user API Key has access to all API endpoints. If you want to limit operations which can be performed by Terraform, you can limit the API Key to certain endpoints. But for this document, a super user API Key is assumed.

Copy the key before pressing the Save button.
//TODO: screenshot http://fusionauth-terraform:9011/admin/api-key/

=== Initializing Terraform Working Directory

With the copied API Key from your FusionAuth environment we initialize the Terraform working directory. Create a new directory. Any name will do, but for consistency, it is suggested to name it after your FusionAuth instance host name.

[source]
----
mkdir your.fusionauth.instance
cd your.fusionauth.instance
----

Next, add the https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/[FusionAuth Terraform Provider] configuration in the directory by:

* Opening the https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs[FusionAuth Terraform Provider Documentation]
* Clicking the [uielement]#USE PROVIDER# button
* Copying the Hashicorp Configuration Language (HCL) provided
* Creating a Terraform configuration file named `main.tf`
* Pasting the copied HCL into that file
* Adding the previously created key as `api_key` to the `provider` section
* Adding the value of the FusionAuth instance you plan to manage ad the `host` variable in the `provider` section
* Make sure host is the full URL including protocol of your FusionAuth instance, in the docker example this may be `http://localhost:9011`

Here's what your `main.tf` file might look like after these steps.

[source]
----
terraform {
  required_providers {
    fusionauth = {
      source = "gpsinsight/fusionauth"
      version = "0.1.94"
    }
  }
}

provider "fusionauth" {
  api_key = "yqTs2LSMd23DYgwbPBA_AxmXHm7_qNFT9jbi3wL9Jr65ymNkNnFisAvjY"
  host = "https://auth.example.com"
}
----

Finally, the following command performs several different initialization steps in order to prepare the current working directory for use with Terraform.

[source]
----
terraform init
----

It is always safe to run https://developer.hashicorp.com/terraform/cli/commands/init[init] multiple times, to bring the working directory up to date with changes in the configuration. Though subsequent runs may give errors, this command will never delete your existing configuration or state.

=== Configuration Strategy

Once you initialize the Terraform working directory, you should review different strategies to handle the import of default FusionAuth configuration which will be discussed below.

[cols="2,4,4"]
|===
| Strategy | Advantages | Disadvantages

| import | manage existing resources | some resources dont fully support the Terraform lifecycle
| data source  | reference existing resources without having to manage them | requires a external process to manage these resources
| create | create and then manage all resources through Terraform | bigger initial effort
|===

== Terraform Import

Before we cover creating, updating and removing, let's look at the the https://developer.hashicorp.com/terraform/language/import[Import] and https://developer.hashicorp.com/terraform/language/data-sources[Data Source] functionality. 

If you have existing FusionAuth configuration you want to manage via Terraform, you will need to import it.

[NOTE.note]
====
The most consistent method is to create all resources through Terraform. In certain scenarios you must use use `Import` or `Data Source` to handle default configuration.
====
// The import and generate config command works and I've tested it, but the import configuration has a issue https://github.com/gpsinsight/terraform-provider-fusionauth/issues/214

There are link:/docs/v1/tech/reference/limitations#default-configuration[configuration elements] that are present in every FusionAuth installation. If you want to manage changes to these via Terraform, you must tell Terraform about them. 

The FusionAuth default Tenant and default Application are two of these configuration elements that you will almost certainly want to modify.

=== Importing The Default Tenant
The default Tenant is created during the initial deployment of FusionAuth. To manage it through Terraform, you must import it. In addition, if you are managing the FusionAuth application, which is the administrative user interface, it will always be in the default Tenant.

In many cases, you can use the default Tenant for all your Applications.
//knowledge source https://fusionauth.io/community/forum/topic/1725/what-are-the-pros-and-cons-of-using-the-default-tenant

[NOTE.note]
====
When using Terraform, it is easier to create your own tenant and applications related to your tenant instead of importing the default ones.
====

As outlined in the https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/tenant[Tenant Terraform Resource], add the following code to `main.tf`.

To make things easier, you only have to update the first Id in the import section with the default Tenant Id. You can find this Id in the FusionAuth Admin UI under `Tenants`. Replace `Replace-This-With-The-Existing-Default-Tenant-Id` with that value.

All other UUID definitions are set to `00000000-0000-0000-0000-000000000000`. You'll find the correct values for the other Ids later.

You can use this technique with any resource you want to import into Terraform.


//TODO: Screenshot Tenant Id

[source]
----
import {
  to = fusionauth_tenant.Default
  id = "Replace-This-With-The-Existing-Default-Tenant-Id"
}

resource "fusionauth_tenant" "Default" {
  name = "Default"
  issuer = "acme.com"
  theme_id = "00000000-0000-0000-0000-000000000000"
  external_identifier_configuration {
    authorization_grant_id_time_to_live_in_seconds = 30
    change_password_id_generator {
      length = 32
      type   = "randomBytes"
    }
    change_password_id_time_to_live_in_seconds = 600
    device_code_time_to_live_in_seconds        = 300
    device_user_code_id_generator {
      length = 6
      type   = "randomAlphaNumeric"
    }
    email_verification_id_generator {
      length = 32
      type   = "randomBytes"
    }
    email_verification_id_time_to_live_in_seconds      = 86400
    email_verification_one_time_code_generator {
      length = 6
      type   = "randomAlphaNumeric"
    }
    external_authentication_id_time_to_live_in_seconds = 300
    one_time_password_time_to_live_in_seconds          = 60
    passwordless_login_generator {
      length = 32
      type   = "randomBytes"
    }
    passwordless_login_time_to_live_in_seconds = 180
    registration_verification_id_generator {
      length = 32
      type   = "randomBytes"
    }
    registration_verification_id_time_to_live_in_seconds = 86400
    registration_verification_one_time_code_generator {
      length = 6
      type   = "randomAlphaNumeric"
    }
    saml_v2_authn_request_id_ttl_seconds = 300
    setup_password_id_generator {
      length = 32
      type   = "randomBytes"
    }
    setup_password_id_time_to_live_in_seconds   = 86400
    two_factor_id_time_to_live_in_seconds       = 300
    two_factor_one_time_code_id_generator {
      length = 6
      type   = "randomDigits"
    }
    two_factor_trust_id_time_to_live_in_seconds = 2592000
  }
  jwt_configuration {
    refresh_token_time_to_live_in_minutes              = 43200
    time_to_live_in_seconds                            = 3600
    refresh_token_revocation_policy_on_login_prevented = true
    refresh_token_revocation_policy_on_password_change = true
    access_token_key_id                                = "00000000-0000-0000-0000-000000000000"
    id_token_key_id                                    = "00000000-0000-0000-0000-000000000000"
  }
  login_configuration {
    require_authentication = true
  }
  email_configuration {
    default_from_email                  = "change-me@example.com"
    default_from_name                   = "FusionAuth"
    host                                = "localhost"
    implicit_email_verification_allowed = true
    port                                = 25
    security                            = "NONE"
    verification_strategy               = "ClickableLink"
    verify_email                        = false
    verify_email_when_changed           = false
    forgot_password_email_template_id   = "00000000-0000-0000-0000-000000000000"
    passwordless_email_template_id      = "00000000-0000-0000-0000-000000000000"
    set_password_email_template_id      = "00000000-0000-0000-0000-000000000000"
  }
}
----

Since you provided a valid Tenant Id, you can let Terraform find the other Ids. Then you can update `main.tf` with those values. Do this by running `terraform plan`.

[source]
----
terraform plan | grep 0000
----

Because of the way Terraform works, it is suggesting replacing the UUID definitions for configuration with `00000000-0000-0000-0000-000000000000`. You don't want to do that. Instead, copy the UUIDs to `main.tf` and replace the `00000000-0000-0000-0000-000000000000`s with them.

Here is how the output of the `grep` command might look:

[source]
----
~ theme_id                           = "75a068fd-e94b-451a-9aeb-3ddb9a3b5987" -> "00000000-0000-0000-0000-000000000000"
~ forgot_password_email_template_id  = "03c264a2-9c89-4fb2-a78b-4333b9485d99" -> "00000000-0000-0000-0000-000000000000"
~ passwordless_email_template_id     = "8e95e7ca-104a-48ce-86d1-aa39e2c71d51" -> "00000000-0000-0000-0000-000000000000"
~ set_password_email_template_id     = "6752116b-9f34-4e66-98ef-438258edeb18" -> "00000000-0000-0000-0000-000000000000"
~ access_token_key_id                = "12c6b146-89cf-9473-490b-1c97e4e68674" -> "00000000-0000-0000-0000-000000000000"
~ id_token_key_id                    = "092dbedc-30af-4149-9c61-b578f2c72f59" -> "00000000-0000-0000-0000-000000000000"
----

Make sure you copy all those Id's from your output to the `main.tf` file. For example, modify `jwt_configuration` to look like

\```
jwt_configuration {
    refresh_token_time_to_live_in_minutes              = 43200
    time_to_live_in_seconds                            = 3600
    refresh_token_revocation_policy_on_login_prevented = true
    refresh_token_revocation_policy_on_password_change = true
    access_token_key_id                                = "12c6b146-89cf-9473-490b-1c97e4e68674"
    id_token_key_id                                    = "092dbedc-30af-4149-9c61-b578f2c72f59"
  }
\```




Once appended run https://developer.hashicorp.com/terraform/cli/commands/plan[terraform plan] to check the validity of your configuration. If there is no output, that indicates that the remote FusionAuth configuration and the `main.tf` file are in sync.

[NOTE.note]
====
The example configuration above is subject to change either by manual changes via the admin UI or version differences such as new Tenant configuration defaults. Carefully check the `terraform plan` output.
====

If the plan is valid and you are happy with the changes run https://developer.hashicorp.com/terraform/cli/commands/apply[terraform apply]

[source]
----
terraform plan
terraform apply
----

[WARNING]
====
Once imported, Terraform tracks the resource in your state file. You can then manage the imported resource similar to any other by updating its attributes.

You can't destroy the default tenant. In FusionAuth this cannot be deleted. Attempting to do so will break your Terraform state. See <<Terraform Remove>> for more details.
====

Leave the import block in your configuration as a record of the default tenant resource’s origin. The import block records that Terraform imported the resource and did not create it.

=== Importing The FusionAuth Application
Next, follow the same process to import the default FusionAuth application which provides the FusionAuth admin user interface. You'll use the https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/application[Application Terraform Resource] and add the following to `main.tf`. 

[source]
----
import {
  to = fusionauth_application.FusionAuth
  id = "3c219e58-ed0e-4b18-ad48-f4f92793ae32"
}

resource "fusionauth_application" "FusionAuth" {
  tenant_id = fusionauth_tenant.Default.id
  name = "FusionAuth"
}
----
Once appended run https://developer.hashicorp.com/terraform/cli/commands/plan[terraform plan] to check the validity of your configuration.

If the plan is valid and you are happy with the changes run https://developer.hashicorp.com/terraform/cli/commands/apply[terraform apply]

[source]
----
terraform plan
terraform apply
----

[WARNING]
====
Once imported, Terraform tracks the resource in your state file. You can then manage the imported resource similar to any other by updating its attributes.

You can't destroy the default FusionAuth application. In FusionAuth this cannot be deleted. Attempting to do so will break your Terraform state. See <<Terraform Remove>> for more details.
====

Leave the import block in your configuration as a record of the default application resource’s origin. The import block records that Terraform imported the resource and did not create it.

=== Terraform Data Source Instead Of Import

Instead of importing a resource you can use an existing FusionAuth configuration as a `Data Source`, the list of all supported data sources you can find in the https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs[FusionAuth Terraform Provider Documentation].

If you choose to manage the Default tenant and FusionAuth application outside Terraform manually or via a script using the link:/docs/v1/tech/client-libraries/[client libraries], but still need to add applications in to the default Tenant via Terraform, you can reference it in `main.tf` as a data source.

* https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/data-sources/tenant[Tenant Data Source documentation]
* https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/data-sources/application[Terraform Data Source: Application]

Here's an example of how you might add such data sources to your Terraform file.
[source]
----
data "fusionauth_tenant" "Default" {
  name = "Default"
}

data "fusionauth_application" "FusionAuth" {
  name = "FusionAuth"
}
----

[NOTE.note]
====
If you manage the default Tenant and FusionAuth Application outside of Terraform but want specific actions (scripts, API calls, etc.) integrated and triggered by your Terraform configuration, you could make the use of https://developer.hashicorp.com/terraform/language/resources/provisioners/syntax[Provisioners].

Terraform includes the concept of provisioners as a measure of pragmatism and last resort since provisioners are non-declarative and potentially unpredictable. They exist because there are always certain behaviors that cannot be directly represented in Terraform's declarative model.
====

Now that you've set up Terraform to handle default configuration, let's walk through creating, updating and removing other resources.
== Terraform Create

To create a new resource you can go through the list of resources available to you in the https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs[FusionAuth Terraform Provider Documentation] and pick the resource you are interested in. Each resource contains information about required and optional arguments.

In this example we create an application called forum in the Default tenant with related roles (admin and user), users (forum-user1 and forum-admin1) and according registrations:

* https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/application[Terraform Application Resource]
* https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/application_role[Terraform Application Role Resource]
* https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/user[Terraform User Resource]
* https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/registration[Terraform Registration Resource]

This example assumes you are using a data source to reference the default tenant. If you imported the default tenant, remove the prefix `data.` to have this example work correctly.

[source]
----
resource "fusionauth_application" "forum" {
  tenant_id = data.fusionauth_tenant.Default.id
  name = "forum"
}

resource "fusionauth_application_role" "forum_admin_role" {
  application_id = fusionauth_application.forum.id
  is_default     = false
  is_super_role  = true
  name           = "admin"
}

resource "fusionauth_application_role" "forum_user_role" {
  application_id = fusionauth_application.forum.id
  is_default     = true
  is_super_role  = false
  name           = "user"
}

resource "fusionauth_user" "forum-user1" {
  email                    = "forum-user1@email.internal"
  first_name               = "John"
  last_name                = "Doe"
  middle_name              = "William"
  password_change_required = true
  password                 = "%WLTvrsYELsyPqC^R7FMUNxt##VyDf6XaWk2R7!gS$oL76Ww"
  username_status          = "ACTIVE"
}

resource "fusionauth_user" "forum-admin1" {
  email                    = "forum-admin1@email.internal"
  first_name               = "John"
  last_name                = "Doe"
  middle_name              = "William"
  password_change_required = true
  password                 = "@CfosPAVT3&hCzz5c^&#2F5BxNUY$X!@s!7Wx9bd6Yon54e3"
  username_status          = "ACTIVE"
}

resource "fusionauth_registration" "forum-admin1-admin-role" {
  user_id        = fusionauth_user.forum-admin1.id
  application_id = fusionauth_application.forum.id
  roles          = ["admin"]
}

resource "fusionauth_registration" "forum-user1-user-role" {
  user_id        = fusionauth_user.forum-user1.id
  application_id = fusionauth_application.forum.id
  roles          = ["user"]
}
----

Once you are happy with your configuration run `terraform plan` and if you are not experiencing errors and are ok with the planned changes you can go ahead with `terraform apply`.

== Terraform Update

Once a resource is either created or imported you can change `main.tf` according to the documentation.

* https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/tenant[Terraform Tenant Resource]
* https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/application[Terraform Application Resource]
* https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/application_role[Terraform Application Role Resource]
* https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/user[Terraform User Resource]
* https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/registration[Terraform Registration Resource]

After you are done with your configuration changes, run `terraform plan` to check the planned changes and review potential errors. After that you can go ahead with `terraform apply`.

If you want to know what already has been defined by Terraform but not specified in your `.tf` files you can run `terraform show`.

The configuration can get very large and if you want to show only very specific resources you can list all resources with `terraform state list` and show the resource state with `terraform state show <resource-name>` accordingly.

=== Ignore Lifecycle Changes
//source: ignore lifecycle changes https://itnext.io/how-and-when-to-ignore-lifecycle-changes-in-terraform-ed5bfb46e7ae
Every time you plan new changes you could face the challenge that information has been updated in your FusionAuth installation. You can either align your configuration with the installation or decide to https://developer.hashicorp.com/terraform/language/meta-arguments/lifecycle#ignore_changes[ignore_changes] in your configuration.

There are two very common examples in FusionAuth resources. One if you manage users through Terraform, where you during creation set password_change_required to true. Or where the user starts changing personal information through FusionAuth or Application.

[source]
----
resource "fusionauth_user" "forum-user1" {
  email                    = "forum-user1@email.internal"
  first_name               = "John"
  last_name                = "Doe"
  middle_name              = "William"
  password_change_required = true
  password                 = "%WLTvrsYELsyPqC^R7FMUNxt##VyDf6XaWk2R7!gS$oL76Ww"
  username_status          = "ACTIVE"
  lifecycle {
    ignore_changes = [
      data,
      image_url,
      password
    ]
  }
}

resource "fusionauth_user" "forum-admin1" {
  email                    = "forum-admin1@email.internal"
  first_name               = "John"
  last_name                = "Doe"
  middle_name              = "William"
  password_change_required = true
  password                 = "@CfosPAVT3&hCzz5c^&#2F5BxNUY$X!@s!7Wx9bd6Yon54e3"
  username_status          = "ACTIVE"
  lifecycle {
    ignore_changes = [
      data,
      image_url,
      password
    ]
  }
}
----

Another example is where your e.g. business application adds user driven data to the FusionAuth Application.

[source]
----
resource "fusionauth_application" "forum" {
  tenant_id = data.fusionauth_tenant.Default.id
  name = "forum"
  lifecycle {
    ignore_changes = [
      data
    ]
  }
}
----

If you are only interested in initially creating and destroying resources you can as well ignore all changes.

[source]
----
lifecycle {
  ignore_changes = all
}
----

== Terraform Remove

If you want to remove a resource, comment out or delete the lines defining the resource in `main.tf`. Run `terraform plan` to double check. Then run `terraform apply`, which will destroy the resource.

[WARNING]
====
If you run https://developer.hashicorp.com/terraform/cli/commands/destroy[terraform destroy] you need to be aware that all Terraform managed resources will be destroyed.
====

=== Prevent Destroy
If you still want to make sure that certain resources arent destroyed, you can specify the https://developer.hashicorp.com/terraform/language/meta-arguments/lifecycle#prevent_destroy[prevent_destroy] lifecycle meta-argument as a measure of safety against the accidental replacement of objects that may be costly to reproduce. However, it will make certain configuration changes impossible to apply, and will prevent the use of the `terraform destroy` command once such objects are created, and so this option should be used sparingly.

[source]
----
lifecycle {
  prevent_destroy = true
}
----

A good example would be an in-use FusionAuth tenant. Deleting a tenant removes all of the associated applications, groups and users.

The same meta-argument you can use for Terraform managed non-deletable resources like Default tenant and FusionAuth application as Terraform would not be able to anyway.
