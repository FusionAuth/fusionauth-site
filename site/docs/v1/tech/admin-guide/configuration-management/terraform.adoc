---
layout: doc
title: Terraform
description: Manage FusionAuth configuration changes over time with Terraform.
navcategory: admin
prerequisites: FusionAuth
technology: Terraform
---

== Overview

In this section we dig deeper in the the community supported, open source link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/[FusionAuth Terraform Provider] with which you will be able to manage FusionAuth using Terraform, an open source infrastructure as code automation tool.

Since it is a community project, it is not as complete as the client libraries. If you find yourself needing to manage something not currently supported, you can contribute to the https://github.com/gpsinsight/terraform-provider-fusionauth[project using GitHub].

* <<Prerequisites>>
* <<Initial Setup>>
** <<FusionAuth API Key>>
** <<Initializing Terraform Working Directory>>
* <<Terraform Import>>
** <<Importing The Default Tenant>>
** <<Importing The FusionAuth Application>>
** <<Terraform Data Source Instead Of Import>>
* <<Terraform Create>>
* <<Terraform Update>>
** <<Ignore Lifecycle Changes>>
* <<Terraform Remove>>
** <<Prevent Destroy>>

== Prerequisites

In order to get the most value from this guide, you should have a FusionAuth instance set up and Terraform CLI ready in your runtime.

If you don't have this set up yet, please review the following links:

* link:/docs/v1/tech/5-minute-setup-guide[5 minute setup guide] - get a FusionAuth instance up and running with a simple application.
* To use Terraform CLI you will need to install it. The link:https://developer.hashicorp.com/terraform/tutorials/docker-get-started/install-cli[Install Terraform] part in the link:https://developer.hashicorp.com/terraform/tutorials/docker-get-started[Get Started - Docker] guide from Terraform will explain you how.

== Initial Setup

In the initial setup we go through the initializing of the Terraform working directory with the FusionAuth API key.

=== FusionAuth API Key

Create an unlimited super user API Key as described in link:/docs/v1/tech/apis/authentication#managing-api-keys[Managing API Keys] by not selecting any endpoint methods to create a super user key. A super user API Key has access to all API endpoints. Copy the key before pressing the Save button.
//TODO: screenshot http://fusionauth-terraform:9011/admin/api-key/

=== Initializing Terraform Working Directory

With the copied API Key from your FusionAuth environment we initialize the Terraform working directory. By creating a new directory named after your FusionAuth instance domain name.

[source]
----
mkdir your.fusionauth.instance
cd your.fusionauth.instance
----

And add the link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/[FusionAuth Terraform Provider] configuration in the directory by:

* Open the link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs[FusionAuth Terraform Provider Documentation]
* Click USE PROVIDER and Copy Information
* Create the `main.tf` Terraform configuration with the copied information
* Add the previously created key as api_key and add the host as configuration options
* Make sure host is the full URL including protocol of your FusionAuth instance, in the docker example this may be `http://localhost:9011`

[source]
----
terraform {
  required_providers {
    fusionauth = {
      source = "gpsinsight/fusionauth"
      version = "0.1.94"
    }
  }
}

provider "fusionauth" {
  api_key = "yqTs2LSMd23DYgwbPBA_AxmXHm7_qNFT9jbi3wL9Jr65ymNkNnFisAvjY"
  host = "https://your.fusionauth.domain"
}
----

Finally, the following command performs several different initialization steps in order to prepare the current working directory for use with Terraform.

[source]
----
terraform init
----

It is always safe to run link:https://developer.hashicorp.com/terraform/cli/commands/init[init] multiple times, to bring the working directory up to date with changes in the configuration. Though subsequent runs may give errors, this command will never delete your existing configuration or state.

=== Configuration Strategy

Once we initialized the Terraform working directory we need to understand the different strategies to handle the configuration which we describe in detail in the following sections.

[cols="2,4,4"]
|===
| Strategy | Advantages | Disadvantages

| import | manage existing resources | some resources dont fully support the Terraform lifecycle
| data source  | reference existing resources without having to manage them | requires a external process to manage these resources
| create | create and then manage all resources through Terraform | bigger initial effort
|===

== Terraform Import

Before we go in to Creating, Updating and Removing of resources we first look in to the link:https://developer.hashicorp.com/terraform/language/import[Import] and link:https://developer.hashicorp.com/terraform/language/data-sources[Data Source] functionality. If you have existing resources you want to manage via Terraform you need to import the current configuration to be able to manage it via Terraform.

[NOTE.note]
====
The cleaner way is to create all resources through Terraform instead, but in certain scenarios you will have to use `Import` or `Data Source` and we go through a few examples in this and the next section.
====
//Additional input for documentation tbd. import cli https://developer.hashicorp.com/terraform/cli/import import config https://developer.hashicorp.com/terraform/language/import https://spacelift.io/blog/importing-exisiting-infrastructure-into-terraform  https://medium.com/swlh/importing-existing-infrastructure-into-terraform-a6ae168ad2bb https://stackoverflow.com/questions/47613926/import-all-resources-defined-in-tf-file https://www.bitslovers.com/terraform-import/)
// The import and generate config command works and I've tested it, but the import configuration has a issue https://github.com/gpsinsight/terraform-provider-fusionauth/issues/214

=== Importing The Default Tenant
Importing the Default tenant is required if you want to manage it through Terraform as it's created during the initial deployment of FusionAuth. In addition, if you are managing FusionAuth application, it will always be part of the Default tenant. As well as there is no need to create a different tenant for creating applications.
//knowledge source https://fusionauth.io/community/forum/topic/1725/what-are-the-pros-and-cons-of-using-the-default-tenant

[NOTE.note]
====
With the usage of Terraform we suggest you to create your own tenant and applications related to your tenant instead of importing.
====

Based on the link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/tenant[Terraform Resource: Tenant] append the `main.tf` configuration file with the following code and replace the UUIDs with the related Ids of your FusionAuth Instance.

To make things easier, we only replace the first Id in the import section with the `Default Tenant Id`. You can find this Id in the FusionAuth Admin UI under `Tenants`. We leave all the other UUID definitions 00000000-0000-0000-0000-000000000000 as they are and replace them in the next step.


//TODO: Screenshot Tenant Id

[source]
----
import {
  to = fusionauth_tenant.Default
  id = "Replace-This-With-The-Existing-Default-Tenant-Id"
}

resource "fusionauth_tenant" "Default" {
  name = "Default"
  issuer = "acme.com"
  theme_id = "00000000-0000-0000-0000-000000000000"
  external_identifier_configuration {
    authorization_grant_id_time_to_live_in_seconds = 30
    change_password_id_generator {
      length = 32
      type   = "randomBytes"
    }
    change_password_id_time_to_live_in_seconds = 600
    device_code_time_to_live_in_seconds        = 300
    device_user_code_id_generator {
      length = 6
      type   = "randomAlphaNumeric"
    }
    email_verification_id_generator {
      length = 32
      type   = "randomBytes"
    }
    email_verification_id_time_to_live_in_seconds      = 86400
    email_verification_one_time_code_generator {
      length = 6
      type   = "randomAlphaNumeric"
    }
    external_authentication_id_time_to_live_in_seconds = 300
    one_time_password_time_to_live_in_seconds          = 60
    passwordless_login_generator {
      length = 32
      type   = "randomBytes"
    }
    passwordless_login_time_to_live_in_seconds = 180
    registration_verification_id_generator {
      length = 32
      type   = "randomBytes"
    }
    registration_verification_id_time_to_live_in_seconds = 86400
    registration_verification_one_time_code_generator {
      length = 6
      type   = "randomAlphaNumeric"
    }
    saml_v2_authn_request_id_ttl_seconds = 300
    setup_password_id_generator {
      length = 32
      type   = "randomBytes"
    }
    setup_password_id_time_to_live_in_seconds   = 86400
    two_factor_id_time_to_live_in_seconds       = 300
    two_factor_one_time_code_id_generator {
      length = 6
      type   = "randomDigits"
    }
    two_factor_trust_id_time_to_live_in_seconds = 2592000
  }
  jwt_configuration {
    refresh_token_time_to_live_in_minutes              = 43200
    time_to_live_in_seconds                            = 3600
    refresh_token_revocation_policy_on_login_prevented = true
    refresh_token_revocation_policy_on_password_change = true
    access_token_key_id                                = "00000000-0000-0000-0000-000000000000"
    id_token_key_id                                    = "00000000-0000-0000-0000-000000000000"
  }
  login_configuration {
    require_authentication = true
  }
  email_configuration {
    default_from_email                  = "change-me@example.com"
    default_from_name                   = "FusionAuth"
    host                                = "localhost"
    implicit_email_verification_allowed = true
    port                                = 25
    security                            = "NONE"
    verification_strategy               = "ClickableLink"
    verify_email                        = false
    verify_email_when_changed           = false
    forgot_password_email_template_id   = "00000000-0000-0000-0000-000000000000"
    passwordless_email_template_id      = "00000000-0000-0000-0000-000000000000"
    set_password_email_template_id      = "00000000-0000-0000-0000-000000000000"
  }
}
----

Now as we provided our `Tenant Id` we let Terraform find our other Ids which we have to replace in the `main.tf` file.

[source]
----
terraform plan | grep 0000
----

It wants to replace several UUID definitions with 00000000-0000-0000-0000-000000000000, but we don't want to do that, instead we will copy the UUID's to our `main.tf` file and replace the defined UUID's with them.

Here is how the output should look like:

[source]
----
~ theme_id                           = "75a068fd-e94b-451a-9aeb-3ddb9a3b5987" -> "00000000-0000-0000-0000-000000000000"
~ forgot_password_email_template_id  = "03c264a2-9c89-4fb2-a78b-4333b9485d99" -> "00000000-0000-0000-0000-000000000000"
~ passwordless_email_template_id     = "8e95e7ca-104a-48ce-86d1-aa39e2c71d51" -> "00000000-0000-0000-0000-000000000000"
~ set_password_email_template_id     = "6752116b-9f34-4e66-98ef-438258edeb18" -> "00000000-0000-0000-0000-000000000000"
~ access_token_key_id                = "12c6b146-89cf-9473-490b-1c97e4e68674" -> "00000000-0000-0000-0000-000000000000"
~ id_token_key_id                    = "092dbedc-30af-4149-9c61-b578f2c72f59" -> "00000000-0000-0000-0000-000000000000"
----

Make sure you copy all those Id's from your output to the `main.tf` file.

Once appended run link:https://developer.hashicorp.com/terraform/cli/commands/plan[terraform plan] to check the validity of your configuration.

[NOTE.note]
====
The example configuration is subject to change either by your manual changes or version related differences over time. Carefully checking the `terraform plan` output will help you understand these.
====

If the plan is valid and you are happy with the changes run link:https://developer.hashicorp.com/terraform/cli/commands/apply[terraform apply]

[source]
----
terraform plan
terraform apply
----

[WARNING]
====
Once imported, Terraform tracks the resource in your state file. You can then manage the imported resource similar to any other by updating its attributes.

With the exception of destroying the default tenant, which normally works in Terraform lifecycle but as in FusionAuth this is a non-deletable tenant this wont work with Terraform either and will break your Terraform state. See <<Terraform Remove>> for more details.
====

We suggest to leave the import block in your configuration a record of the resource's origin. The import block records that Terraform imported the resource and did not create it.

=== Importing The FusionAuth Application
The same works with importing the default FusionAuth application based on the link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/application[Terraform Resource: Application] by appending the following code to `main.tf`.

[source]
----
import {
  to = fusionauth_application.FusionAuth
  id = "00000000-0000-0000-0000-000000000000"
}

resource "fusionauth_application" "FusionAuth" {
  tenant_id = fusionauth_tenant.Default.id
  name = "FusionAuth"
}
----
Replace the 00000000-0000-0000-0000-000000000000 with your `FusionAuth Application Id`, you can find it in the `FusionAuth Admin UI` under `Applications`.

Once appended run link:https://developer.hashicorp.com/terraform/cli/commands/plan[terraform plan] to check the validity of your configuration.

If the plan is valid and you are happy with the changes run link:https://developer.hashicorp.com/terraform/cli/commands/apply[terraform apply]

[source]
----
terraform plan
terraform apply
----

[WARNING]
====
Once imported, Terraform tracks the resource in your state file. You can then manage the imported resource similar to any other by updating its attributes.

With the exception of destroying the FusionAuth application, which normally works in Terraform lifecycle but as in FusionAuth this is a non-deletable application this wont work with Terraform either and will break your Terraform state. See <<Terraform Remove>> for more details.
====

We suggest to leave the import block in your configuration, a record of the resource's origin. The import block records that Terraform imported the resource and did not create it.

=== Terraform Data Source Instead Of Import

Instead of importing a resource you can use an existing FusionAuth configuration as a `Data Source`, the list of all supported data sources you can find in the link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs[FusionAuth Terraform Provider Documentation]

In the scenario where you choose to manage the Default tenant and FusionAuth application outside Terraform, but still add applications in to the default Tenant via Terraform, you can reference it in the configuration as a data source.

* link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/data-sources/tenant[Terraform Data Source: Application]
* link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/data-sources/application[Terraform Data Source: Application]

[source]
----
data "fusionauth_tenant" "Default" {
  name = "Default"
}

data "fusionauth_application" "FusionAuth" {
  name = "FusionAuth"
}
----

[NOTE.note]
====
If you manage the Default tenant and FusionAuth application outside of Terraform but want specific actions (scripts, API calls, etc.) still integrated and triggered by your Terraform configuration, you could make the use of link:https://developer.hashicorp.com/terraform/language/resources/provisioners/syntax[Provisioners].

Terraform includes the concept of provisioners as a measure of pragmatism and last resort (Since provisioners are non-declarative and potentially unpredictable), knowing that there are always certain behaviors that cannot be directly represented in Terraform's declarative model.
====
//TODO Provisioner https://developer.hashicorp.com/terraform/language/resources/provisioners/syntax

== Terraform Create

To create a new resource you can go through the list of resources available to you in the link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs[FusionAuth Terraform Provider Documentation] and pick the resource you are interested in. Each resource contains information about required and optional arguments.

In this example we create an application called forum in the Default tenant with related roles (admin and user), users (forum-user1 and forum-admin1) and according registrations:

* link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/application[Terraform Resource: Application]
* link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/application_role[Terraform Resource: Application Role]
* link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/user[Terraform Resource: User]
* link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/registration[Terraform Resource: Registration]

For referenced Ids which are `Data Sources` use a leading `data.` for the specification e.g. `tenant_id = data.fusionauth_tenant.Default.id`, if you used the import without using `Data Sources` you have to remove the leading `data.` from the following resource definitions, otherwise `terraform plan` will throw an error.

[source]
----
resource "fusionauth_application" "forum" {
  tenant_id = data.fusionauth_tenant.Default.id
  name = "forum"
}

resource "fusionauth_application_role" "forum_admin_role" {
  application_id = fusionauth_application.forum.id
  is_default     = false
  is_super_role  = true
  name           = "admin"
}

resource "fusionauth_application_role" "forum_user_role" {
  application_id = fusionauth_application.forum.id
  is_default     = true
  is_super_role  = false
  name           = "user"
}

resource "fusionauth_user" "forum-user1" {
  email                    = "forum-user1@email.internal"
  first_name               = "John"
  last_name                = "Doe"
  middle_name              = "William"
  password_change_required = true
  password                 = "%WLTvrsYELsyPqC^R7FMUNxt##VyDf6XaWk2R7!gS$oL76Ww"
  username_status          = "ACTIVE"
}

resource "fusionauth_user" "forum-admin1" {
  email                    = "forum-admin1@email.internal"
  first_name               = "John"
  last_name                = "Doe"
  middle_name              = "William"
  password_change_required = true
  password                 = "@CfosPAVT3&hCzz5c^&#2F5BxNUY$X!@s!7Wx9bd6Yon54e3"
  username_status          = "ACTIVE"
}

resource "fusionauth_registration" "forum-admin1-admin-role" {
  user_id        = fusionauth_user.forum-admin1.id
  application_id = fusionauth_application.forum.id
  roles          = ["admin"]
}

resource "fusionauth_registration" "forum-user1-user-role" {
  user_id        = fusionauth_user.forum-user1.id
  application_id = fusionauth_application.forum.id
  roles          = ["user"]
}
----

Once you are happy with your configuration run `terraform plan` and if you are not experiencing errors and are ok with the planned changes you can go ahead with `terraform apply`.

== Terraform Update

Once a resource is either created or imported you can change your main.tf according to the documentation.

* link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/tenant[Terraform Resource: Tenant]
* link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/application[Terraform Resource: Application]
* link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/application_role[Terraform Resource: Application Role]
* link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/user[Terraform Resource: User]
* link:https://registry.terraform.io/providers/gpsinsight/fusionauth/latest/docs/resources/registration[Terraform Resource: Registration]

After you are done with your configuration changes, run `terraform plan` to check the planned changes and review potential errors. After that you can go ahead with `terraform apply`.

If you want to know what already has been defined by Terraform but not specified in your `.tf` files you can run `terraform show`.

The configuration can get very large and if you want to show only very specific resources you can list all resources with `terraform state list` and show the resource state with `terraform state show <resource-name>` accordingly.

=== Ignore Lifecycle Changes
//source: ignore lifecycle changes https://itnext.io/how-and-when-to-ignore-lifecycle-changes-in-terraform-ed5bfb46e7ae
Every time you plan new changes you could face the challenge that information has been updated in your FusionAuth installation. You can either align your configuration with the installation or decide to link:https://developer.hashicorp.com/terraform/language/meta-arguments/lifecycle#ignore_changes[ignore_changes] in your configuration.

There are two very common examples in FusionAuth resources. One if you manage users through Terraform, where you during creation set password_change_required to true. Or where the user starts changing personal information through FusionAuth or Application.

[source]
----
resource "fusionauth_user" "forum-user1" {
  email                    = "forum-user1@email.internal"
  first_name               = "John"
  last_name                = "Doe"
  middle_name              = "William"
  password_change_required = true
  password                 = "%WLTvrsYELsyPqC^R7FMUNxt##VyDf6XaWk2R7!gS$oL76Ww"
  username_status          = "ACTIVE"
  lifecycle {
    ignore_changes = [
      data,
      image_url,
      password
    ]
  }
}

resource "fusionauth_user" "forum-admin1" {
  email                    = "forum-admin1@email.internal"
  first_name               = "John"
  last_name                = "Doe"
  middle_name              = "William"
  password_change_required = true
  password                 = "@CfosPAVT3&hCzz5c^&#2F5BxNUY$X!@s!7Wx9bd6Yon54e3"
  username_status          = "ACTIVE"
  lifecycle {
    ignore_changes = [
      data,
      image_url,
      password
    ]
  }
}
----

Another example is where your e.g. business application adds user driven data to the FusionAuth Application.
Remember to remove the `.data` in case you don't use `Data Sources`.

[source]
----
resource "fusionauth_application" "forum" {
  tenant_id = data.fusionauth_tenant.Default.id
  name = "forum"
  lifecycle {
    ignore_changes = [
      data
    ]
  }
}
----

If you are only interested in initially creating and destroying resources you can as well ignore all changes.

[source]
----
lifecycle {
  ignore_changes = all
}
----

== Terraform Remove

If you want to remove a resource you can comment or delete the lines accordingly. Run `terraform plan` and `terraform apply` once modified which will then destroy the commented or deleted ressource.

If you run link:https://developer.hashicorp.com/terraform/cli/commands/destroy[terraform destroy] you need to be aware that all Terraform managed resources will be destroyed.

=== Prevent Destroy
If you still want to make sure that certain resources arent destroyed, you can specify the link:https://developer.hashicorp.com/terraform/language/meta-arguments/lifecycle#prevent_destroy[prevent_destroy] lifecycle meta-argument as a measure of safety against the accidental replacement of objects that may be costly to reproduce. However, it will make certain configuration changes impossible to apply, and will prevent the use of the `terraform destroy` command once such objects are created, and so this option should be used sparingly.

[source]
----
lifecycle {
  prevent_destroy = true
}
----

A good example would be a productive FusionAuth tenant, as deleting a tenant removes all of the associated applications, groups and users.

The same meta-argument you can use for Terraform managed non-deletable resources like Default tenant and FusionAuth application as Terraform would not be able to anyway.
