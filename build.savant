/*
 * Copyright (c) 2016-2019, Inversoft Inc., All Rights Reserved
 */


project(group: "io.fusionauth", name: "fusionauth-site", version: "2.0.2", licenses: ["ApacheV2_0"]) {
  workflow {
    standard()
  }

  publishWorkflow {
    subversion(repository: "http://svn.inversoft.org/savant")
  }

  publications {
    main {
      publication(name: project.name, type: "css", file: "_site/assets/css/fusionauth.css")
      publication(name: project.name, type: "zip", file: "build/fusionauth-scss.zip")
    }
  }
}

release = loadPlugin(id: "org.savantbuild.plugin:release-git:1.0.0")
dependency = loadPlugin(id: "org.savantbuild.plugin:dependency:1.0.2")
file = loadPlugin(id: "org.savantbuild.plugin:file:1.0.2")

target(name: "pre-install", description: "Gets any dependencies required to build") {
  if (!cleanExecute(['bundle', 'install'])) {
    fail("Unable to run 'bundle install' for FusionAuth Site. Are you certain you have Jekyll installed?")
  }

  if (!cleanExecute(['npm', 'install'])) {
    fail("Unable to run 'npm install' for FusionAuth Site. Are you certain you have npm/node installed?")
  }
}

target(name: "clean", description: "cleans the project", dependsOn: ["pre-install"]) {
  cleanExecute(["bundle", "exec", "jekyll", "clean"])
}

target(name: "compile", description: "builds the project", dependsOn: ["pre-install"]) {
  if (switches.has("drafts")) {
    cleanExecute(["bundle", "exec", "jekyll", "build", "--drafts"])

  } else {
    cleanExecute(["bundle", "exec", "jekyll", "build"])
  }
}

target(name: "serve", description: "Build and watch the project", dependsOn: ["pre-install"]) {
  cleanExecute(["bundle", "exec", "jekyll", "serve", "--drafts", "--incremental", "-o"])
}

target(name: "watch", description: "Build and watch the project", dependsOn: ["pre-install"]) {
  cleanExecute(["bundle", "exec", "jekyll", "build", "--drafts", "--incremental", "--watch"])
}

target(name: "int", description: "Releases a local integration build of the project", dependsOn: ["compile"]) {
  file.copy(to: "build/scss") {
    fileSet(dir: "site/_sass", includePatterns: [~/.+\.scss/])
  }

  file.rename {
    fileSet(dir: "build/scss")
    filter(token: "_", value: "") // Remove the underscores
  }

  file.zip(file: "build/fusionauth-scss.zip") {
    fileSet(dir: "build/scss")
  }

  dependency.integrate()
}

target(name: "release", description: "Releases a full version of the project", dependsOn: ["int"]) {
  release.release()
}

target(name: "push", description: "Push the site to fusionauth.io") {
  // Skip checking Git if we are forced to do so (use the force Luke)
  if (!switches.has("force")) {
    output.infoln("Checking git")
    if (!cleanExecute(["git", "diff-index", "--quiet", "origin/master", "--"])) {
      output.infoln("You must commit and push your changes before running this command. (Use --force to skip this check, but keep in mind that your local changes won't be visible until they are pushed)")
      exit 1
    }
  }

  // Allow the command line switch --user to specify a different user on the SSH command
  def host = "www-1.fusionauth.io"
  if (switches.has("user")) {
    host = switches.values("user")[0] + "@" + host
  }

  output.infoln("Deploying...")
  cleanExecute(["ssh", "-t", host, "sudo -H -u www-data -g www-data /var/git/fusionauth-site/deploy.sh"])
}

boolean cleanExecute(List<String> args) {
  ProcessBuilder pb = new ProcessBuilder(args).inheritIO().directory(new File('.'))
  clearEnvironment(pb)

  def process = pb.start()
  process.consumeProcessOutput(System.out, System.err)
  process.waitFor()
  return process.exitValue() == 0
}

/**
 * Clears out the RBENV variables in the environment.
 *
 * @param pb The ProcessBuilder to clear out.
 */
static def clearEnvironment(ProcessBuilder pb) {
  pb.environment().remove("RBENV_VERSION")
  pb.environment().remove("RBENV_DIR")
  pb.environment().remove("RBENV_ROOT")
}
